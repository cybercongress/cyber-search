/* Gradle plugins part */
buildscript {
    ext {
        kotlinVersion = "1.1.61"
        kotlinCoroutinesVersion = "0.19.3"
        jacksonVersion = "2.8.9"
        undertowVersion = "1.4.20.Final"
        kafkaVersion = "0.11.0.1"
        cassandraConfluentVersion = "3.3.0"
        web3jVersion = "2.3.0"
        rxkotlinVersion = "2.1.0"
        apacheHttpClientVersion = "4.1.3"
        ehcacheVersion = "3.4.0"
        elasticVersion = "2.4.6"

        // plugins
        dependencyPluginVersion = "1.0.3.RELEASE"
        shadowPluginVersion = "2.0.1"

        // tests
        junitVersion = "5.0.0"
        junitPlatformVersion = "1.0.0"
        mockitoVersion = "2.1.0"
        mockitoKotlinVersion = "0.7.0"

        // logs
        slf4jVersion = "1.7.25"
        lockbackVersion = "1.2.3"
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/'}
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("io.spring.gradle:dependency-management-plugin:$dependencyPluginVersion")
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion")
        classpath("com.github.jengelman.gradle.plugins:shadow:$shadowPluginVersion")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
    distributionType = Wrapper.DistributionType.ALL
}


subprojects {
    apply plugin: "kotlin"
    apply plugin: "org.junit.platform.gradle.plugin"
    apply plugin: "io.spring.dependency-management"

    kotlin {
        experimental {
            coroutines "enable"
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependency("org.slf4j:slf4j-api:$slf4jVersion")
            dependency("ch.qos.logback:logback-classic:$lockbackVersion")

            dependency("org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion")
            dependency("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
            dependency("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinCoroutinesVersion")

            dependency("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
            dependency("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")

            dependency("io.undertow:undertow-core:$undertowVersion")
            dependency("org.apache.httpcomponents:httpasyncclient:$apacheHttpClientVersion")

            dependency("org.apache.kafka:kafka-clients:$kafkaVersion")
            dependency("org.apache.kafka:kafka-streams:$kafkaVersion")
            dependency("org.apache.kafka:connect-api:$kafkaVersion")

            dependency("org.junit.jupiter:junit-jupiter-api:$junitVersion")
            dependency("org.junit.platform:junit-platform-launcher:$junitPlatformVersion")
            dependency("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
            dependency("org.mockito:mockito-core:$mockitoVersion")
            dependency("com.nhaarman:mockito-kotlin:$mockitoKotlinVersion")

            dependency("org.ehcache:ehcache:$ehcacheVersion")
            dependency("org.elasticsearch:elasticsearch:$elasticVersion") {
                exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
            }

            dependency("com.datastax.cassandra:cassandra-driver-core:$cassandraConfluentVersion")
            dependency("com.datastax.cassandra:cassandra-driver-mapping:$cassandraConfluentVersion")
            dependency("com.datastax.cassandra:cassandra-driver-extras:$cassandraConfluentVersion")

            dependency("org.web3j:core:$web3jVersion")
            dependency("io.reactivex.rxjava2:rxkotlin:$rxkotlinVersion")
        }
    }

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api")
        testCompile("org.junit.platform:junit-platform-launcher")
        testCompile("org.mockito:mockito-core")
        testCompile("com.nhaarman:mockito-kotlin")
        testRuntime("org.junit.jupiter:junit-jupiter-engine")
    }
}


project(":common") {
    dependencies {
        compile("org.slf4j:slf4j-api")
        compile("ch.qos.logback:logback-classic")

        compile("org.jetbrains.kotlin:kotlin-stdlib-jre8")
        compile("org.jetbrains.kotlin:kotlin-reflect")
        compile("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8")

        compile("com.fasterxml.jackson.core:jackson-databind")
        compile("com.fasterxml.jackson.module:jackson-module-kotlin")

        compile("org.apache.kafka:kafka-clients")
        compile("com.datastax.cassandra:cassandra-driver-core")
        compile("com.datastax.cassandra:cassandra-driver-mapping")
        compile("com.datastax.cassandra:cassandra-driver-extras")

    }
}


project(":search-api") {
    apply plugin: "application"

    dependencies {
        compile(project(":common"))
        compile(project(":dao-service"))
        compile("io.undertow:undertow-core")
        compile("org.elasticsearch:elasticsearch")
    }

    mainClassName = "fund.cyber.search.SearchApiApplication"
}

project(":dao-service") {
    dependencies {
        compile(project(":common"))
        compile("org.ehcache:ehcache")
        compile("org.apache.httpcomponents:httpasyncclient")

        //dependencies for jaxb
        compile 'javax.xml.bind:jaxb-api:2.3.0'
        compile 'org.glassfish.jaxb:jaxb-runtime:2.3.0'
        compile 'javax.activation:activation:1.1.1'
    }
}

project(":pumps") {
    dependencies {
        compile project(":common")
        compile project(":dao-service")
        compile("org.web3j:core")
        compile("io.reactivex.rxjava2:rxkotlin")
    }
}